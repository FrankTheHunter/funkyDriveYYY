buildscript {
    ext.kotlin_version = '1.5.31'
    ext.ktor_version = '1.6.4'
    ext.mockk_version = '1.12.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    if (name == "applications" || name == "components") return

    group "io.collective"

    apply plugin: 'kotlin'

    defaultTasks "clean", "build"

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "io.ktor:ktor-server-jetty:$ktor_version"
        implementation "io.ktor:ktor-html-builder:$ktor_version"

        implementation "ch.qos.logback:logback-classic:1.2.6"

        testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
        testImplementation "io.mockk:mockk:$mockk_version"
        testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    }

    sourceSets {
        main.kotlin.srcDir "src/main/kotlin"
        main.resources.srcDir "src/main/resources"

        test.kotlin.srcDir "src/test/kotlin"
        test.resources.srcDir "src/test/resources"
    }

    // needed for heroku
    task stage(dependsOn: ['build', 'clean'])
    build { mustRunAfter "clean" }
    gradle.taskGraph.whenReady {
        taskGraph ->
            if (taskGraph.hasTask(stage)) {
                test.enabled = false
            }
    }
}